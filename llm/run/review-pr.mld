---
description: Automated PR review workflow for mlld registry
mlld-version: ">=1.3.0"
---

# ðŸ¤– Automated Module Review

/import { MLLD_PR_NUMBER, MLLD_REPO_OWNER, MLLD_REPO_NAME, MLLD_GITHUB_TOKEN, MLLD_ANTHROPIC_API_KEY } from @input
/import { env } from @mlld/env
/import { github } from @mlld/github
/import { extractModuleFromPR, validateModule } from "../modules/registry-utils.mld"
/import { queryClaudeAPI, parseReviewResponse, createFallbackReview } from "../modules/claude-utils.mld"
/import { createReviewContext } from "../templates/review-context.mld"
/import { reviewPrompt } from "../templates/review-prompt.mld"
/import { formatGitHubReview } from "../templates/github-review.mld"
/import { createSummaryReport } from "../templates/summary-report.mld"

## Configuration

/var @repo = `@MLLD_REPO_OWNER/@MLLD_REPO_NAME`

## Fetch PR Data

/var @prData = @github.pr.view(@MLLD_PR_NUMBER, @repo)
/var @prFiles = @github.pr.files(@MLLD_PR_NUMBER, @repo)
/var @prDiff = @github.pr.diff(@MLLD_PR_NUMBER, @repo)
## Extract Module

/var @moduleContent = @extractModuleFromPR(@prFiles, @prDiff)
/when @moduleContent.error => /show `ERROR: Module extraction failed: @moduleContent.error`

/var @moduleSourceContent = <@moduleContent.source.url>

/var @moduleValidation = @validateModule(@moduleContent)

## Prepare Review

/var @reviewContext = @createReviewContext(@prData, @moduleContent, @moduleValidation, @moduleSourceContent)


/var @prompt = @reviewPrompt(@reviewContext)
/var @claudeResponse = @queryClaudeAPI(@prompt, 3)
/var @aiReview = @parseReviewResponse(@claudeResponse)

## Post Review

/var @githubReview = @formatGitHubReview(@aiReview, @reviewContext)


>> Post the review to GitHub
/show "Posting review to GitHub..."
/show `PR: #@MLLD_PR_NUMBER`
/show `Repo: @repo`

/exe @getRecommendation(@review) = js {
  return review.recommendation.toLowerCase();
}
/var @recommendation = @getRecommendation(@aiReview)
/show `Recommendation: @recommendation`

/var @reviewResult = @github.pr.review(@MLLD_PR_NUMBER, @repo, @recommendation, @githubReview)
/show "Review Result:"
/show @reviewResult

/var @summary = @createSummaryReport(@prData, @moduleValidation, @aiReview)
/show @summary
